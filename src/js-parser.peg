{
  var none,
  parser = this,
  entities = {},
  entity = Entity();

  function Entity(){
    return {
      description: '',
      short_description: ''
    };
  }

  function indent_shift(source){
    var min_indent = -1;
    /* calculate minimum number of initial spaces */
    source.replace(/(?:^|\n)([\ \t]*)[^\s]/g, function(_, indent){
      min_indent = min_indent < 0 ? indent.length : Math.min(min_indent, indent.length);
    });
    return min_indent > 0
    /* remove redundant initial spaces */
      ? source.replace(new RegExp('(^|\\n)([\\ \\t]{1,' + min_indent + '})', 'g'), '$1')
      : source;
  }

  function throwError(){
    var offset = Math.max(pos.offset, rightmostFailuresPos.offset);
    var found = offset < input.length ? input.charAt(offset) : null;
    var errorPosition = pos.offset > rightmostFailuresPos.offset ? pos : rightmostFailuresPos;

    throw new parser.SyntaxError(
      cleanupExpected(rightmostFailuresExpected),
      found,
      offset,
      errorPosition.line,
      errorPosition.column
    );
  }
}

file
  = chunk* { return entities; }

chunk
  = entity
  / ANY

entity
  = OPEN
    ( documentation / &{ throwError(); } )
    CLOSE !{
      /* capture line number workaround */
      if(typeof line !== 'undefined'){
        entity.line = line + 1;
      }
    }
  {
    entities[entity.id] = entity;
    /* init new supposed entity */
    entity = Entity();
  }

OPEN
  = ( $ / &{ return 0 === offset; } ) _* '/**' _*

CLOSE
  = _* ( '**/' / '*/' &{ throw new parser.SyntaxError(['**/'], '*/', offset, line, column); } )

documentation
  = tags_container
    heading addition*
    description_container?

tags_container
  = tags? $

heading
  = signatures
  / class
  / mixin
  / section
  / namespace

addition
  = includes
  / fires

description_container
  = EMPTY_LINE description_text?

tags
  = tag ( _* ',' _* tags )?

tag
  = id:'read-only' {
    entity.readonly = true;
  }
  / id:( 'internal' / 'chainable' ) {
    entity[id] = true;
  }
  / id:( 'section' / 'alias of' / 'related to' / 'belongs to' ) _* ':' _* name:name {
    /* `alias of' -> `alias_of' */
    entity[id.replace(/\ /g, '_')] = name;
  }
  / 'deprecated' ( _* ':' _* since:version _* '..' _* off:version { entity.deprecated = {since: since, off:off}; }
                   / _* ':' _* since:version { entity.deprecated = {since: since}; }
                   / { entity.deprecated = true; } )

version
  = first:[0-9]+ rest:( '.' v:version { return v; } )? {
    return first.join('') + (rest ? '.' + rest : '');
  }

description_text
/* Multiline description block
   Empty lines are allowed here */
  = text:( indent:INDENT nested:TEXT $ { return indent + nested; } / EMPTY_LINE )+ {
    entity.description = indent_shift(text.join('\n'));
    // short description lasts until the first empty line
    entity.short_description = entity.description.replace(/\n\n[\s\S]*$/, '');
  }

signatures
/* Signatures consist of bare signatures block and followed argument descriptions block */
  = bare_signatures argument_descriptions?

class
  = INDENT 'class' _+ id:name superclass:( _* '<' _* nested:name { return nested; } )? $ {
    entity.id = id;
    entity.type = 'class';
    entity.superclass = superclass || none;
  }

mixin
  = INDENT 'mixin' _+ id:name $ {
    entity.id = id;
    entity.type = 'mixin';
  }

section
  = INDENT '==' _* id:name _* '==' $ {
    entity.id = id;
    entity.type = 'section';
  }

namespace
  = INDENT id:name $ {
    entity.id = id;
    entity.type = 'namespace';
  }

includes
  = INDENT 'includes' _+ includes:names $ {
    entity.included_mixins = includes;
  }

fires
  = INDENT 'fires' _+ events:events $ {
    entity.events = events;
  }

events
  = first:event rest:( ',' nested:events { return nested; } )? {
    rest = rest || [];
    rest.unshift(first);
    return rest;
  }

event
  = first:NAME rest:( sp:( ':' / '.' / '@' / '-' ) id:event { return sp + id; } )? {
    return first + rest;
  }

name_or_namespace
  = first:NAME rest:( '.' nested:name_or_namespace { return nested; } )? {
    return first + (rest ? '.' + rest : '');
  }

name
  = first:name_or_namespace rest:( '@' event
                                   / '#' NAME
                                   / ) {
    return first + rest.join('');
  }

names
  = first:name rest:( _* ',' _* names:names { return names; } )? {
    var names = rest || [];
    names.unshift(first);
    return names;
  }

bare_signatures
/* Bare signatures one signature per line */
  = INDENT signature $ bare_signatures?
  {
    entity.signatures = entity.signatures || [];
    entity.signatures.unshift({
      args: entity.args,
      returns: entity.returns
    });
    
    delete entity.args;
    delete entity.returns;
  }

signature
  = method
  / constructor
  / property
  / constant

method
  = method_entry results?

constructor
  = 'new' _+ method {
    entity.id = entity.id + '.new';
    entity.type = 'constructor';
  }

property
  = id:name results {
    entity.id = id;
    entity.type = 'property';
  }

constant
  = id:name _* '=' _* results:result_entries {
    entity.id = id;
    entity.type = 'constant';
    entity.returns = results;
  }

method_entry
  = id:name _* '(' _* bound:'@'? _* args:args? _* ')' {
    entity.id = id;
    entity.type = 'method';
    entity.args = args || [];
    if(bound){
      entity.bound = true;
    }
  }

args
  = first:arg? opt:optional_args? rest:( _* ',' _* args:args { return args; } )? {
    rest = rest || [];
    if(opt){
      var arg;
      for(; arg = opt.pop(); ){
        arg.optional = true;
        rest.unshift(arg);
      }
    }
    if(first){
      rest.unshift(first);
    }
    return rest;
  }

optional_args
/* Optional arguments subsequence */
  = _* '[' _* ( ',' _* )? first:arg opt:optional_args? rest:( _* ',' _* args:args { return args; } )? _* ']' {
    rest = rest || [];
    if(opt){
      rest = opt.concat(rest);
    }
    rest.unshift(first);
    return rest;
  }

arg
/* Required argument */
  = bare_arg
/* Optional argument */
  / '[' _* arg:bare_arg _* ']' {
    arg.optional = true;
    return arg;
  }

bare_arg
  = name:NAME args:( _* '(' _* a:args _* ')' { return a; } )?
  ext:( _* '=' _* value:VALUE { return {value:value}; } /* with default value */
        / _* '...' { return {ellipsis:true}; } /* with ellipsis */
      )?
  {
    var arg = {
      name: name
    };
    if(args){
      arg.args = args;
    }
    if(none !== ext.value){
      arg.default_value = ext.value;
    }
    if(ext.ellipsis){
      arg.ellipsis = true;
    }
    return arg;
  }

results
  = _* '->' _* results:result_entries {
    entity.returns = results;
  }

result_entries
/* anything */
  = '?' {
    return [
      {type: '?'}
    ];
  }
  / first:VALUE rest:( _* '|' _* nested:result_entries { return nested; } )? {
    rest = rest || [];
    var ret = {
      type: String(first)
    };
    if(first.array){
      ret.array = true;
    }
    if(first.ellipsis){
      ret.ellipsis = true;
    }
    rest.unshift(ret);
    return rest;
  }

argument_descriptions
/* Argument descriptions block, which may be appended to bare signatures block */
  = INDENT argument_description $ argument_descriptions?

argument_description
  = '-' _* name:NAME
  types:( _* '(' _* ntypes:argument_description_types _* ')' { return ntypes; } )?
  desc:( _* ':' _* ndesc:argument_description_text? { return ndesc; } )? {
    (entity.arguments = entity.arguments || []).push({
      name: name,
      types: types || ['mixed'],
      description: desc || ''
    });
  }

argument_description_types
  = '?' { return []; }
  / name:name rest:( _* '|' _* alts:argument_description_types { return alts; } )? {
    var alts = rest || [];
    alts.unshift(name);
    return alts;
  }

argument_description_text
/* `first' is a text up to end of line
   `rest' is an optional text lines (in case of multiline description) without end of last line
   An empty lines are disallowed here because it's used as a separator for description block. */
  = first:TEXT rest:( $ indent:INDENT !( '-' / 'includes' / 'fires' ) nested:TEXT { return indent + nested; } )* {
    return first + (rest.length ? '\n' + indent_shift(rest.join('\n')) : '');
  }

VALUE
  = QUOTED_STRING
  / NULL
  / NUMBER
  / BOOLEAN
  / REGEXP
  / ARRAY
  / OBJECT
  / name

QUOTED_STRING
/* double quoted strings */
  = [\"] content:( [\\] [\"] { return '\\"'; } / [\\] [\n] { return '\n'; } / [^\"\n] )* [\"] {
    return content.join('');
  }
/* single quoted strings */
  / [\'] content:( [\\] [\'] { return '\\\''; } / [\\] [\n] { return '\n'; } / [^\'\n] )* [\'] {
    return content.join('');
  }

NULL
  = 'null'i NAME_EDGE { return null; }

NUMBER
  = int_sign:[-]? int_part:( first:[1-9] rest:[0-9]+ { return first + rest.join(''); } / [0-9] )
    frac_part:( '.' digits:[0-9]+ { return '.' + digits.join(''); } )?
    exp_part:( [eE] sign:[\-\+]? digits:[0-9]+ { return 'e' + ('-' === sign ? sign : '') + digits.join(''); } )?
  { return parseFloat(int_sign + int_part + frac_part + exp_part); }

BOOLEAN
  = 'true'i NAME_EDGE { return true; }
  / 'false'i NAME_EDGE { return false; }

REGEXP
  = '/' expression:( '\\/' / [^/] )* '/' flags:[gim]* {
    return new RegExp(expression.join(''), flags.join(''));
  }

ARRAY
  = '[' values:ARRAY_ENTRIES? ellipsis:'...'? ']' {
    value = values || [];
    values.array = true;
    if(ellipsis){
      values.ellipsis = true;
    }
    return values;
  }

OBJECT
  = '{' values:OBJECT_ENTRIES? '}' {
    return values || {};
  }

ARRAY_ENTRIES
  = first:VALUE rest:( _* ',' _* next:ARRAY_ENTRIES { return next; } )? {
    rest = rest || [];
    rest.unshift(first);
    return rest;
  }

OBJECT_ENTRIES
  = key:KEY _* ':' _* value:VALUE rest:( _* ',' _* next:OBJECT_ENTRIES { return next; } )? {
    rest = rest || {};
    rest[key] = value;
    return rest;
  }

NAME
  = first:[$_a-zA-Z] rest:[$_a-zA-Z0-9]* {
    return first + rest.join('');
  }

NAME_EDGE
  = ![$_a-zA-Z0-9]

KEY
  = QUOTED_STRING
  / NAME

TEXT
/* A text up to end of line */
  = chars:[^\r\n]+ {
    return chars.join('');
  }

ANY
  = .

INDENT
  = _$ spacing:_+ { return spacing.join(''); }

EMPTY_LINE
  = _$ _* $ { return ''; }

_ /* white space */
  = [\ \t]

_$
  /* line begin */
  = _* '*'

$ /* line end */
  = [\r]? [\n]
